plugins {
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.springframework.boot' version '3.5.4' apply false
}

allprojects {
    group = "com.postforge"
    version = "0.1.0"

    repositories {
        mavenCentral()
    }
}

subprojects {
    plugins.withId('java') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }

        plugins.withId('java-library') {
            java {
                withSourcesJar()
                withJavadocJar()
            }
        }

        plugins.withId('io.spring.dependency-management') {
            dependencyManagement {
                imports {
                    mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.4"
                }
            }
        }

        tasks.withType(JavaCompile).configureEach {
            options.encoding = 'UTF-8'
        }

        tasks.withType(Javadoc).configureEach {
            options.encoding = 'UTF-8'
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok:1.18.34'
            annotationProcessor 'org.projectlombok:lombok:1.18.34'
            testCompileOnly 'org.projectlombok:lombok:1.18.34'
            testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
        }

        tasks.withType(Test).configureEach {
            useJUnitPlatform()
        }


    }


}

