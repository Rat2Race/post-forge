plugins {
    id 'org.springframework.boot' version '3.5.4' apply false
}

ext {
    springBootVersion = '3.5.4'
    lombokVersion = '1.18.34'
}

allprojects {
    group = "com.postforge"
    version = "0.1.0"

    repositories {
        mavenCentral()
    }
}

subprojects {

    plugins.withType(JavaPlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }

        dependencies {
            implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

            compileOnly "org.projectlombok:lombok:${lombokVersion}"
            annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
            testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
            testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        }

        tasks.withType(JavaCompile).configureEach {
            options.encoding = 'UTF-8'
        }

        tasks.withType(Javadoc).configureEach {
            options.encoding = 'UTF-8'
            options.addStringOption('Xdoclint:none', '-quiet')
        }

        tasks.named('test') {
            useJUnitPlatform()
        }
    }

    plugins.withType(JavaLibraryPlugin) {
        java {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

